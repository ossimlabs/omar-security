
pluginManagement {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core/" }
        gradlePluginPortal()
    }
    plugins {
        id "org.grails.grails-web" version "6.0.0"
        id "org.grails.grails-gsp" version "6.0.0"
        id "com.bertramlabs.asset-pipeline" version "4.3.0"
    }
}

rootProject.name="omar-security"

[ 'lib', 'plugin', 'app' ].each { String projectType ->
    File projectTypeDir = new File( rootDir, "${ projectType }s" )

    if ( projectTypeDir.exists() ) {
        projectTypeDir.eachDirMatch( { it.endsWith( projectType ) } ) { File subprojectDir ->
            include subprojectDir.name
            project( ":${ subprojectDir.name }" ).projectDir = subprojectDir
        }
    }
}

if ( System.getenv( 'O2_INLINE_BUILD' ) ) {
    def modules = [
    ]

    modules?.each { module ->
      File moduleDir = "${rootProject.projectDir.parent}/${module}" as File

      if ( moduleDir.exists() ) {
        includeBuild  moduleDir.absolutePath
      }
    }
}
